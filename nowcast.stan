functions {
  // Convolve a pdf and case vector using matrix multiplication
  vector convolve(vector cases, vector pdf) {
    int t = num_elements(cases);
    matrix[t, t] delay_mat = rep_matrix(0, t, t);
    int max_pdf = num_elements(pdf) + 1;
    row_vector[max_pdf] row_pdf = to_row_vector(append_row(pdf, 0.0));
    vector[t] convolved_cases;
    
    for (s in 1:t) {
      int max_length = min(s, max_pdf);
      delay_mat[s, (s - max_length + 1):s] = row_pdf[(max_pdf - max_length + 1):max_pdf];
    }
  
   convolved_cases = delay_mat * to_vector(cases);
   
   //Initialise first entry as slightly above 0
   convolved_cases[1] = 0.00001;

   return convolved_cases;
  }

  real discretised_lognormal_pmf(int y, real mu, real sigma) {

    return((lognormal_cdf(y, mu, sigma) - lognormal_cdf(y - 1, mu, sigma)));
  }
  
  
  real discretised_gamma_pmf(int y, real mu, real sigma) {
    
    // calculate alpha and beta for gamma distribution
    real alpha = (mu / sigma)^2;
    real beta = (sigma^2) / mu;
    
    return((lognormal_cdf(y, alpha, beta) - lognormal_cdf(y - 1, alpha, beta)));
  }
}


data {
  int t;                             // number of time steps
  int day_of_week[t];                // day of the week indicator (1 - 7)
  int <lower = 0> cases[t];          // observed cases
  vector<lower = 0>[t] shifted_cases;// mean delay + incubation shifted cases (smoothed)
  real inc_mean_sd;                  // prior sd of mean incubation period
  real inc_mean_mean;                // prior mean of mean incubation period
  real inc_sd_mean;                  // prior sd of sd of incubation period
  real inc_sd_sd;                    // prior sd of sd of incubation period
  int max_inc;                       // maximum incubation period
  real rep_mean_mean;                // prior mean of mean reporting delay
  real rep_mean_sd;                  // prior sd of mean reporting delay
  real rep_sd_mean;                  // prior mean of sd of reporting delay
  real rep_sd_sd;                    // prior sd of sd of reporting delay
  int max_rep;                       // maximum report delay
  real <lower = 0> r_mean;           // prior mean of reproduction number
  real <lower = 0> r_sd;             // prior standard deviation of reproduction number
  real gt_mean_sd;                   // prior sd of mean generation time
  real gt_mean_mean;                 // prior mean of mean generation time
  real gt_sd_mean;                   // prior sd of sd of generation time
  real gt_sd_sd;                     // prior sd of sd of generation time
  int max_gt;                        // maximum generation time
  int model_type;                    // type of model: 1 = poisson otherwise negative binomial
}

transformed data{
  int<lower = 0> weekly_cases[t];    // weekly observed cases
  int<lower = 0> cum_cases[t];       // cumulative cases
  real r_alpha;                      // alpha parameter of the R gamma prior
  real r_beta;                       // beta parameter of the R gamma prior

  // calculate alpha and beta for gamma distribution
  r_alpha = (r_mean / r_sd)^2;
  r_beta = (r_sd^2) / r_mean;
  
  // calculate weekly cases
  cum_cases[1] = cases[1];
  weekly_cases[1] = cases[1];
  for (s in 2:t) { 
    cum_cases[s] = cum_cases[s - 1] + cases[s];
    weekly_cases[s] = cum_cases[s] - cum_cases[max(1, s - 7)];
  }
  
}
parameters{
  vector<lower = 0>[t] noise;        // noise on the mean shifted observed cases
  real <lower = 0> inc_mean;         // mean of incubation period
  real <lower = 0> inc_sd;           // sd of incubation period
  real <lower = 0> rep_mean;         // mean of reporting delay
  real <lower = 0> rep_sd;           // sd of incubation period
  real<lower = 0> rep_phi;           // overdispersion of the reporting process
  vector[7] day_of_week_eff;         // day of week reporting effect
  vector<lower = 0>[t] R;            // effective reproduction number over time
  real <lower = 0> gt_mean;          // mean of generation time
  real <lower = 0> gt_sd;            // sd of generation time 
  real<lower = 0> inf_phi;           // overdispersion of the infection process
}

transformed parameters {
  vector[max_rep] rev_delay;         // reversed report delay pdf
  vector[max_inc] rev_incubation;    // reversed incubation period pdf
  vector<lower = 0>[t] infections;   // infections over time
  vector<lower = 0>[t] onsets;       // onsets over time
  vector<lower = 0>[t] reports;      // reports over time
  vector<lower = 0>[t] cum_reports;  // cumulative reported cases (unadjusted for reporting)
  vector<lower = 0>[t] weekly_reports;// weekly reported cases
  vector[max_gt] rev_generation_time;// reversed generation time pdf
  vector<lower = 0>[t] infectiousness;// infections over time
  vector<lower = 0>[t] branch_infections;// infections generated by the branching process
  vector<lower = 0>[t] branch_onsets; // onsets generated by the branching process
  vector<lower = 0>[t] branch_reports; // reports generated by the branching process

   
  // reverse the distributions to allow vectorised access
    for (j in 1:max_rep) {
      rev_delay[j] =
        discretised_lognormal_pmf(max_rep - j + 1, inc_mean, inc_sd);
        }
   
    for (j in 1:max_inc) {
      rev_incubation[j] =
        discretised_lognormal_pmf(max_inc - j + 1, rep_mean, rep_sd);
    }

    for (j in 1:(max_gt)) {
      rev_generation_time[j] =
        discretised_gamma_pmf(max_gt - j + 1, gt_mean, gt_sd);
    }
  
  // generate infections from median shifted cases and non-parameteric noise
  infections = shifted_cases .* noise;

  // infectiousness from infections
  infectiousness = convolve(infections, rev_generation_time);
  
  // Estimate infections using branching process
  branch_infections = R .* infectiousness;
  
  // onsets from infections
  onsets = convolve(infections, rev_incubation);
  branch_onsets = convolve(branch_infections, rev_incubation);
  
  // reports from onsets
  reports = convolve(onsets, rev_delay);
  branch_reports = convolve(branch_onsets, rev_delay);
  
 // calculate Cumulative reports
  cum_reports = cumulative_sum(reports);
  
  for (s in 1:t) {
    // calculate weekly reports
    weekly_reports[s] = cum_reports[s] - cum_reports[max(1, s - 7)];
    // add reporting effects
    reports[s] *= day_of_week_eff[day_of_week[s]];
    branch_reports[s] *= day_of_week_eff[day_of_week[s]];
    }
}

model {
  // day of week reporting effect
  for (j in 1:7) {
    day_of_week_eff[j] ~ normal(1, 0.2) T[0,];
  }
  
  // reporting overdispersion
  rep_phi ~ exponential(1);

  // noise on median shift of reported cases for cases by date of infection
  for (i in 1:t) {
    noise[i] ~ normal(1, 0.5) T[0,];
  }
  
  // daily cases given reports
  if (model_type == 1) {
    target += poisson_lpmf(cases | reports);
  }else{
    target += neg_binomial_2_lpmf(cases | reports, rep_phi);
  }
  
  // weekly cases given weekly reports (penalises day of week effect)
  target += poisson_lpmf(weekly_cases[7:t] | weekly_reports[7:t]);

  // penalised priors for incubation period, and report delay
  target += normal_lpdf(inc_mean | inc_mean_mean, inc_mean_sd) * t;
  target += normal_lpdf(inc_sd | inc_sd_mean, inc_sd_sd) * t;
  target += normal_lpdf(rep_mean | rep_mean_mean, rep_mean_sd) * t;
  target += normal_lpdf(rep_sd | rep_sd_mean, rep_sd_sd) * t;
  
  // initial prior on R
  R[1] ~ gamma(r_alpha, r_beta);
  
  // infection overdispersion
  inf_phi ~ exponential(1);
  
  for (s in 2:t) {
    {
      // rescale previous R and overall sd prior for gamma
      real r_alpha_ = (R[s - 1] / r_sd)^2;
      real r_beta_ = (r_sd^2) / R[s - 1];
      // reproduction number prior dependent on previous time step estimate
      R[s] ~ gamma(r_alpha_, r_beta_);
    }
  }
  
  // penalised_prior on generation interval
  target += normal_lpdf(gt_mean | gt_mean_mean, gt_mean_sd) * t;
  target += normal_lpdf(gt_sd | gt_sd_mean, gt_sd_sd) * t;
  
  // Likelihood of Rt given infections
    if (model_type == 1) {
      target += poisson_lpmf(cases | branch_reports);
    }else{
      target += neg_binomial_2_lpmf(cases | branch_reports, inf_phi);
    }
   
}
  
generated quantities {
  int imputed_infections[t];
  
    // Simulated infections
  if (model_type == 1) {
    imputed_infections = poisson_rng(infections);
  }else{
    imputed_infections = neg_binomial_2_rng(infections, rep_phi);
  }

}
